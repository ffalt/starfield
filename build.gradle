import se.bjurr.gitchangelog.plugin.gradle.GitChangelogTask
import se.bjurr.gitchangelog.api.InclusivenessStrategy

// Top-level build file where you can add configuration options common to all sub-projects/modules.
plugins {
    alias(libs.plugins.androidApplication) apply false
    alias(libs.plugins.gitChangelog)
    id 'net.researchgate.release' version '3.0.2'
}

release {
    failOnCommitNeeded = true
    failOnPublishNeeded = false
    failOnSnapshotDependencies = true
    failOnUnversionedFiles = true
    failOnUpdateNeeded = true
    revertOnFail = true
    pushReleaseVersionBranch = 'main'
    tagTemplate = '${version}'
    preCommitText = ''
    preTagCommitMessage = 'Pre Tag '
    tagCommitMessage = 'Tag '
    newVersionCommitMessage = 'Bump version to unreleased '
    snapshotSuffix = '-SNAPSHOT'

    versionPropertyFile = 'gradle.properties'
    versionProperties = []
    buildTasks = ["gitChangelogTask"]
    ignoredSnapshotDependencies = []

    git {
        requireBranch.set('main')
        pushToRemote.set(false) //'origin')
        pushToBranchPrefix.set('')
        commitVersionFileOnly.set(false)
        signTag.set(true)
    }

}

apply from: 'version.gradle'

tasks.register('gitReleaseNotesTask', GitChangelogTask) {
    println "Generating Release notes for " + project.version
    def prev_tag = getCurrentPrevGitVersion(project.version)
    println "Compare to previous " + prev_tag

    toRevision = "HEAD"
    fromRevisionStrategy = InclusivenessStrategy.EXCLUSIVE
    fromRevision = prev_tag
    file = new File("${rootDir}/RELEASE_NOTES.md")
    prependToFile = false // false will replace the file, true will prepend content
    templateContent = """{{#tags}}# Release {{name}} ({{tagDate}})

  {{#ifContainsType commits type='feat'}}
## Features

    {{#commits}}
      {{#ifCommitType . type='feat'}}
 - {{#eachCommitScope .}} **{{.}}** {{/eachCommitScope}} {{{commitDescription .}}} ([{{hash}}](https://github.com/ffalt/starfield/commit/{{hashFull}}))
      {{/ifCommitType}}
    {{/commits}}
  {{/ifContainsType}}
  {{#ifContainsType commits type='fix'}}

## Bug Fixes

    {{#commits}}
      {{#ifCommitType . type='fix'}}
 - {{#eachCommitScope .}} **{{.}}** {{/eachCommitScope}} {{{commitDescription .}}} ([{{hash}}](https://github.com/ffalt/starfield/commit/{{hashFull}}))
      {{/ifCommitType}}
    {{/commits}}
  {{/ifContainsType}}

## Full Changelog

https://github.com/ffalt/starfield/compare/PREVIOUS...{{name}}
{{/tags}}
""".replace("PREVIOUS", prev_tag)
}

tasks.register('gitChangelogTask', GitChangelogTask) {
    fromRevision = "440bdfdf94c09ef4edf1885b25852078a66d6332"
    toRevision = "HEAD"
    templateContent = """# Changelog Starfield Live Wallpaper
{{#tags}}

{{#ifEquals name "Unreleased"}}
## Unreleased
{{else}}
## [{{name}}](https://github.com/ffalt/starfield/compare/{{name}}) ({{tagDate .}})
{{/ifEquals}}

  {{#ifContainsType commits type='feat'}}
### Features

    {{#commits}}
      {{#ifCommitType . type='feat'}}
 - {{#eachCommitScope .}} **{{.}}** {{/eachCommitScope}} {{{commitDescription .}}} ([{{hash}}](https://github.com/ffalt/starfield/commit/{{hashFull}}))
      {{/ifCommitType}}
    {{/commits}}
  {{/ifContainsType}}

  {{#ifContainsType commits type='fix'}}
### Bug Fixes

    {{#commits}}
      {{#ifCommitType . type='fix'}}
 - {{#eachCommitScope .}} **{{.}}** {{/eachCommitScope}} {{{commitDescription .}}} ([{{hash}}](https://github.com/ffalt/starfield/commit/{{hashFull}}))
      {{/ifCommitType}}
    {{/commits}}
  {{/ifContainsType}}
{{/tags}}
 """
}

tasks.register('releaseInfo') {
    def info = "\nVERSION INFO:\n\tFull version name: " + getFullVersionName() +
            "\n\tAndroid version name: " + getAndroidVersionName() +
            "\n\tAndroid version code: " + getAndroidVersionCode() +
            "\n\tAndroid Git branch: " + getGitBranch() +
            "\n\tAndroid Git describe: " + getAndroidGitDescribe() +
            "\n\tGit SHA: " + getGitSha() +
            "\n\tBuild Time: " + getBuildTime() +
            "\n\tLatest Git Tag: " + getCurrentGitRevision() +
            "\n\tPrevious Git Tag: " + getCurrentPrevGitVersion(getCurrentGitRevision()) +
            "\n"
    println(info);
}

tasks.register('preProcess') {
    println "preProcess"
}

beforeReleaseBuild.dependsOn preProcess
