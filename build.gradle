import se.bjurr.gitchangelog.api.InclusivenessStrategy
import se.bjurr.gitchangelog.plugin.gradle.GitChangelogTask

// Top-level build file where you can add configuration options common to all sub-projects/modules.
plugins {
    alias(libs.plugins.androidApplication) apply false
    alias(libs.plugins.gitChangelog)
    alias(libs.plugins.grGit)
}

def templateChangelogContent = """# Changelog Starfield Live Wallpaper
{{#tags}}

{{#ifEquals name "Unreleased"}}
## Unreleased
{{else}}
## [{{name}}](https://github.com/ffalt/starfield/compare/{{name}}) ({{tagDate .}})
{{/ifEquals}}

  {{#ifContainsType commits type='feat'}}
### Features

    {{#commits}}
      {{#ifCommitType . type='feat'}}
 - {{#eachCommitScope .}} **{{.}}** {{/eachCommitScope}} {{{commitDescription .}}} ([{{hash}}](https://github.com/ffalt/starfield/commit/{{hashFull}}))
      {{/ifCommitType}}
    {{/commits}}
  {{/ifContainsType}}

  {{#ifContainsType commits type='fix'}}
### Bug Fixes

    {{#commits}}
      {{#ifCommitType . type='fix'}}
 - {{#eachCommitScope .}} **{{.}}** {{/eachCommitScope}} {{{commitDescription .}}} ([{{hash}}](https://github.com/ffalt/starfield/commit/{{hashFull}}))
      {{/ifCommitType}}
    {{/commits}}
  {{/ifContainsType}}
{{/tags}}
 """

// Template only for a single entry append (no global header)
def templateChangelogEntryContent = """{{#tags}}

{{#ifEquals name "Unreleased"}}
## Unreleased
{{else}}
## [{{name}}](https://github.com/ffalt/starfield/compare/{{name}}) ({{tagDate .}})
{{/ifEquals}}

  {{#ifContainsType commits type='feat'}}
### Features

    {{#commits}}
      {{#ifCommitType . type='feat'}}
 - {{#eachCommitScope .}} **{{.}}** {{/eachCommitScope}} {{{commitDescription .}}} ([{{hash}}](https://github.com/ffalt/starfield/commit/{{hashFull}}))
      {{/ifCommitType}}
    {{/commits}}
  {{/ifContainsType}}

  {{#ifContainsType commits type='fix'}}
### Bug Fixes

    {{#commits}}
      {{#ifCommitType . type='fix'}}
 - {{#eachCommitScope .}} **{{.}}** {{/eachCommitScope}} {{{commitDescription .}}} ([{{hash}}](https://github.com/ffalt/starfield/commit/{{hashFull}}))
      {{/ifCommitType}}
    {{/commits}}
  {{/ifContainsType}}
{{/tags}}
"""

def templateReleaseLogContent = """{{#tags}}{{#ifContainsType commits type='feat'}}## Features

    {{#commits}}
      {{#ifCommitType . type='feat'}}
 - {{#eachCommitScope .}} **{{.}}** {{/eachCommitScope}} {{{commitDescription .}}} ([{{hash}}](https://github.com/ffalt/starfield/commit/{{hashFull}}))
      {{/ifCommitType}}
    {{/commits}}
  {{/ifContainsType}}
  {{#ifContainsType commits type='fix'}}

## Bug Fixes

    {{#commits}}
      {{#ifCommitType . type='fix'}}
 - {{#eachCommitScope .}} **{{.}}** {{/eachCommitScope}} {{{commitDescription .}}} ([{{hash}}](https://github.com/ffalt/starfield/commit/{{hashFull}}))
      {{/ifCommitType}}
    {{/commits}}
  {{/ifContainsType}}

## Full Changelog

https://github.com/ffalt/starfield/compare/PREVIOUS...{{name}}
{{/tags}}
"""

def templateFdroidChangelogContent = """{{#tags}}
    {{#commits}}
{{#ifCommitType . type='feat'}}
 - {{#eachCommitScope .}} **{{.}}** {{/eachCommitScope}} {{{commitDescription .}}}
{{/ifCommitType}}{{#ifCommitType . type='fix'}}
 - {{#eachCommitScope .}} **{{.}}** {{/eachCommitScope}} {{{commitDescription .}}}
{{/ifCommitType}}{{/commits}}
{{/tags}}
"""

static def calculateAndroidVersionCode(versionName) {
    def (major, minor, patch, build) = versionName.replace("v", "").tokenize('.')
    (major, minor, patch, build) = [major, minor, patch, build].collect { it == null ? 0 : it.toInteger() }
    return (major * 1000 * 1000 * 1000) + (minor * 1000 * 1000) + (patch * 1000) + build
}

tasks.register('gitReleaseNotesTask', GitChangelogTask) {
    def prev_tag = ['bash', '-lc', 'git tag --sort=-creatordate | head -n 2 | tail -n 1'].execute().text.trim()
    println "Generating Release notes for ${project.version} compared to ${prev_tag}"

    toRevision.set(project.version)
    fromRevisionStrategy.set(InclusivenessStrategy.EXCLUSIVE)
    fromRevision.set(prev_tag)
    file.set(new File("${rootDir}/RELEASE_NOTES.md"))
    prependToFile.set(false) // false will replace the file, true will prepend content
    templateContent.set(templateReleaseLogContent.replace("PREVIOUS", prev_tag))
}

tasks.register('gitChangelogTask', GitChangelogTask) {
    // Append only the new changes since the latest tag to the top of CHANGELOG.md
    def prev_tag = ['bash', '-lc', 'git tag --sort=-creatordate | head -n 1'].execute().text.trim()
    if (!prev_tag) {
        // Fallback to initial commit if no tag exists
        fromRevision.set("440bdfdf94c09ef4edf1885b25852078a66d6332")
        fromRevisionStrategy.set(InclusivenessStrategy.EXCLUSIVE)
    } else {
        fromRevision.set(prev_tag)
        fromRevisionStrategy.set(InclusivenessStrategy.EXCLUSIVE)
    }
    toRevision.set(project.version)
    file.set(new File("${rootDir}/CHANGELOG.md"))
    prependToFile.set(true)
    templateContent.set(templateChangelogEntryContent)
}

tasks.register('bumpReleaseVersion') {
    if (!grgit.status().clean) {
        throw new GradleException('Git repository is dirty. Commit your changes first!')
    }

    // get next version
    def (major, minor, patch) = project.version.replace("v", "").tokenize('.')
    (major, minor, patch) = [major, minor, patch].collect { it.toInteger() }
    def nextVersion = "v${major}.${minor}.${patch + 1}"

    println("Bumping ${project.version} => ${nextVersion}")

    // update file
    def propsFile = file("${rootDir}/gradle.properties")
    def propsText = propsFile.text.replace(project.version, nextVersion)
    propsFile.text = propsText

    // commit
    grgit.add(patterns: ['gradle.properties'])
    grgit.commit(message: "Bump version to ${nextVersion}", sign: false)
    // sign commit
    'git commit --amend --no-edit --allow-empty -s'.execute() // sign commit
}

tasks.register('prepareReleaseTag') {
    doLast {
        println("Adding temp tag ${project.version}")
        grgit.tag.add(name: project.version)
    }
}

tasks.register('release') {
    dependsOn tasks.named('prepareReleaseTag'), tasks.named('gitChangelogTask'), tasks.named('gitFdroidChangelogTask')

    doLast {
        println("Releasing ${project.version}")
        // remove temp tag
        grgit.tag.remove(names: [project.version])
        // add fdroid changelog files
        // compute the exact fdroid changelog file created earlier
        def versionCode = calculateAndroidVersionCode(project.version)
        def fdroidRelPath = "fastlane/metadata/android/en-US/changelogs/${versionCode}.txt"
        def fdroidFile = file(fdroidRelPath)
        if (fdroidFile.exists()) {
            println("Adding F-Droid changelog ${fdroidRelPath} to git")
            grgit.add(patterns: [fdroidRelPath])
        } else {
            println("Fdroid changelog not found at ${fdroidRelPath} - skipping add")
        }
        // commit changelog
        grgit.commit(message: "Release ${project.version}", all: true, sign: false)
        // sign commit
        'git commit --amend --no-edit --allow-empty -s'.execute()
        // add final release tag
        grgit.tag.add(name: project.version)
        println("To publish the commit and tag: git push --atomic origin main ${project.version}")
    }
}

tasks.register('gitFdroidChangelogTask', GitChangelogTask) {
    println("Update Fdroid changelogs ${project.version}")
    def prev_tag = ['bash', '-lc', 'git tag --sort=-creatordate | head -n 1'].execute().text.trim()
    if (!prev_tag) {
        fromRevision.set("440bdfdf94c09ef4edf1885b25852078a66d6332")
        fromRevisionStrategy.set(InclusivenessStrategy.EXCLUSIVE)
    } else {
        fromRevision.set(prev_tag)
        fromRevisionStrategy.set(InclusivenessStrategy.EXCLUSIVE)
    }
    toRevision.set(project.version)
    def versionCode = calculateAndroidVersionCode(project.version)
    file.set(new File("fastlane/metadata/android/en-US/changelogs/${versionCode}.txt"))
    prependToFile.set(false)
    templateContent.set(templateFdroidChangelogContent)
}
