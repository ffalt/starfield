plugins {
    alias(libs.plugins.androidApplication)
}

def calculateAndroidVersionCode(versionName) {
    def (major, minor, patch, build) = versionName.replace("v", "").tokenize('.')
    (major, minor, patch, build) = [major, minor, patch, build].collect { it == null ? 0 : it.toInteger() }
    return (major * 1000000) + (minor * 10000) + (patch * 100) + build
}

android {
    namespace = "io.github.ffalt.starfield"
    compileSdk = 34

    defaultConfig {
        applicationId = "io.github.ffalt.starfield"
        minSdk = 26
        targetSdk = 34
        versionCode = calculateAndroidVersionCode(project.version)
        versionName = project.version
    }
    signingConfigs {
        debug {
            keyAlias = "androiddebugkey"
            keyPassword = "android"
            storeFile = file("debug.keystore")
            storePassword = "android"
        }
        release {
            keyAlias = System.getenv("SIGNING_KEY_ALIAS")
            keyPassword = System.getenv("SIGNING_KEY_PASSWORD")
            storeFile = file("starfield_keystore.jks")
            storePassword = System.getenv("SIGNING_STORE_PASSWORD")
        }
    }

    buildTypes {
        debug {
            applicationIdSuffix = ".debug"
            signingConfig = signingConfigs.getByName("debug")
            minifyEnabled = false
            shrinkResources = false
        }
        release {
            signingConfig = signingConfigs.getByName("release")
            minifyEnabled = true
            shrinkResources = true
            proguardFiles(
                    getDefaultProguardFile("proguard-android-optimize.txt"),
                    "proguard-rules.pro"
            )
        }
    }
    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_1_8
        targetCompatibility = JavaVersion.VERSION_1_8
    }
}

dependencies {
    implementation(libs.androidx.appcompat)
    implementation(libs.androidx.preference)
    implementation(libs.jaredrummler.colorpicker)
}
